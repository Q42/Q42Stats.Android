plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    signingConfigs {
        adhoc {
            storeFile file('../adhoc.jks')
            storePassword 'q42stats'
            keyAlias 'q42stats'
            keyPassword 'q42stats'
        }
        // since this is just a demo app, we don't need a release config for the Play Store
    }
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.q42.q42stats.sample"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "distribution"

    productFlavors {
        local {
            dimension "distribution"
        }

        jitpack {
            dimension "distribution"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            debuggable true
        }
        // this demo app is not intended for release to the store. The release buildType exists for
        // the sake of matching the library build type
        release {
            minifyEnabled true
            signingConfig signingConfigs.adhoc
            debuggable false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'


    localImplementation project(path: ':library')
    jitpackImplementation 'com.github.Q42:Q42Stats.Android:0.0.4'
}