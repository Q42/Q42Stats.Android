import java.text.SimpleDateFormat

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

group = 'com.github.Q42'

android {
    compileSdkVersion compile_sdk_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion compile_sdk_version

        buildConfigField("String", "LIB_BUILD_DATE", '"' + new SimpleDateFormat("yyyy-MM-dd").format(Calendar.getInstance().getTime()) + '"')

        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}


dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$version_kotlinx_coroutines"
    implementation "androidx.annotation:annotation:$version_androidx_annotation"

    implementation "com.google.android.gms:play-services-wallet:$version_google_wallet"

    testImplementation 'junit:junit:4.13.2'
    //json is included in Android, but not in Kotlin. This import makes it available for unit tests
    testImplementation 'org.json:json:20201115'


}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                // JitPack will override these value for us, so leave them to the defaults.
                // Default group is set in the root of this gradle file
                //groupId = 'com.example.MyLibrary'
                //artifactId = 'final'
                //version = '1.0'
            }
        }
    }
}